if(NOT DEFINED COMIN_DL_LIBS)
  include(CheckFunctionExists)
  if(NOT CMAKE_REQUIRED_QUIET)
    message(CHECK_START "Looking for dlopen")
  endif()
  set(save_CMAKE_REQUIRED_LIBRARIES "${CMAKE_REQUIRED_LIBRARIES}")
  set(save_CMAKE_REQUIRED_QUIET "${CMAKE_REQUIRED_QUIET}")
  set(CMAKE_REQUIRED_QUIET True)
  unset(comin_dlopen_exists CACHE)
  foreach(dl_libs IN ITEMS "" "${CMAKE_DL_LIBS}")
    set(CMAKE_REQUIRED_LIBRARIES "${dl_libs}")
    list(APPEND CMAKE_REQUIRED_LIBRARIES ${save_CMAKE_REQUIRED_LIBRARIES})
    check_function_exists(dlopen comin_dlopen_exists)
    if(comin_dlopen_exists)
      set(COMIN_DL_LIBS "${dl_libs}" CACHE STRING "")
    endif()
    unset(comin_dlopen_exists CACHE)
    if(DEFINED COMIN_DL_LIBS)
      break()
    endif()
  endforeach()
  set(CMAKE_REQUIRED_QUIET "${save_CMAKE_REQUIRED_QUIET}")
  set(CMAKE_REQUIRED_LIBRARIES "${save_CMAKE_REQUIRED_LIBRARIES}")
  if(NOT CMAKE_REQUIRED_QUIET)
    if(DEFINED COMIN_DL_LIBS)
      message(CHECK_PASS "found")
    else()
      message(CHECK_FAIL "not found")
    endif()
  endif()
endif()

if(NOT DEFINED COMIN_DL_LIBS)
  message(FATAL_ERROR
    "Could not find libraries that enable the dlopen function"
  )
endif()
